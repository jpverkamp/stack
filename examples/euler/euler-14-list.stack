# https://projecteuler.net/problem=14
# Which starting number, under one million, produces the longest Collatz chain?

1000000 @bound

make-stack @cache
cache 1 stack-push! 
cache 1 stack-push! 

{
  @n

  # Extend the cache if needed
  n cache stack-size - 2 + @extension
  { 
    @0 !0
    { 
      @0 !0
      cache 0 stack-push! 
    } extension loop 
    "extended cache by " write extension write " to " write n writeln
  }
  { !0 }
  extension 0 > if

  [
    # Already cached
    { cache n stack-ref 0 > } { cache n stack-ref }

    # Base case
    { n 1 <= } 1
    
    # If n is even, divide by 2
    { n 2 % 0 = } { n 2 / collatz-length 1 + }
    
    # Default: If n is odd, multiply by 3 and add 1
    { n 3 * 1 + collatz-length 1 +}
  ] cond
  @l

  cache n l stack-set!
  l
} @collatz-length

# Store best value so far
1 @best-v
1 @best-l

# Check each value under 1 million
{
  @v !0
  v 1 + !v
  v collatz-length @l

  {
    @0 !0
    v !best-v
    l !best-l

    "new best: " write
    v write
    " -> " write
    l writeln
  } 
  { !0 }
  l best-l > if
} bound loop

best-v writeln
