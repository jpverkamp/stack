# https://projecteuler.net/problem=14
# Which starting number, under one million, produces the longest Collatz chain?

1000000 @bound

make-int-hash @cache
cache 1 1 hash-set!

{
  @n
  # Already cached 
  { cache n hash-get }
  # New value
  {
    @0 !1
    # Calculate next Collatz value m
    { n 2 / }
    { n 3 * 1 + }
    n 2 % 0 = if
    @m
    
    # Calculate length recursively
    m collatz-length 1 +
    @l

    # Store in cache and return
    cache n l hash-set!
    l
  }
  cache n hash-has? if
} @collatz-length

# Store best value so far
1 @best-v
1 @best-l

# Check each value under 1 million
{
  @v !0
  v 1 + !v
  v collatz-length @l
  
  {
    @0 !0
    v !best-v
    l !best-l

    "new best: " write
    v write
    " -> " write
    l writeln
  } 
  { !0 }
  l best-l > if
} bound loop

best-v writeln
