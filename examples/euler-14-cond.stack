# https://projecteuler.net/problem=14
# Which starting number, under one million, produces the longest Collatz chain?

make-int-hash @cache
cache 1 1 hash-set!

{
  @n

  [
    # Already cached
    { @0 !1 cache n hash-has? } { @0 !1 cache n hash-get }
    
    # If n is even, divide by 2
    { @0 !1 n 2 % 0 = } { @0 !1 n 2 / collatz-length 1 + }
    
    # Default: If n is odd, multiply by 3 and add 1
    { @0 !1 n 3 * 1 + collatz-length 1 +}
  ] cond
  @l

  cache n l hash-set!
  l
} @collatz-length

# Store best value so far
make-hash @results
results "best-v" 1 hash-set!
results "best-l" 1 hash-set!

# Check each value under 1 million
{
  @v
  v 1 + !v
  v collatz-length @l

  {
    @0 !0
    results "best-v" v hash-set!
    results "best-l" l hash-set!

    "new best: " write
    v write
    " -> " write
    l writeln
  } 
  { @0 !0 }
  l results "best-l" hash-get > if
} 10000 loop

results "best-v" hash-get writeln
